// CGT DSL Grammar
// Each line is either a transaction, a comment, or blank
transaction_list = { SOI ~ (line ~ NEWLINE)* ~ line? ~ EOI }
line = _{ transaction | COMMENT | "" }
transaction = { date ~ command }

command = {
    cmd_buy |
    cmd_sell |
    cmd_dividend |
    cmd_capreturn |
    cmd_split |
    cmd_unsplit
}

cmd_buy = { "BUY" ~ buy_sell_args }
cmd_sell = { "SELL" ~ buy_sell_args }
cmd_dividend = { "DIVIDEND" ~ dividend_args }
cmd_capreturn = { "CAPRETURN" ~ capreturn_args }
cmd_split = { "SPLIT" ~ split_args }
cmd_unsplit = { "UNSPLIT" ~ split_args }

date = { ASCII_DIGIT{4} ~ "-" ~ ASCII_DIGIT{2} ~ "-" ~ ASCII_DIGIT{2} }

// Amount with optional currency code (ISO 4217, 3 uppercase letters)
// Must not match keywords (TAX, EXPENSES, TOTAL, RATIO, BUY, SELL)
money = { decimal ~ currency_code? }
currency_code = @{
    !("TAX" | "BUY") ~
    ASCII_ALPHA_UPPER{3} ~
    !(ASCII_ALPHANUMERIC | "-")
}

// BUY/SELL: ticker amount @ price [currency] [EXPENSES expenses [currency]]
buy_sell_args = { ticker ~ quantity ~ "@" ~ money ~ expenses_clause? }
expenses_clause = { "EXPENSES" ~ money }

// DIVIDEND: ticker amount TOTAL value [currency] TAX tax [currency]
dividend_args = { ticker ~ quantity ~ "TOTAL" ~ money ~ "TAX" ~ money }

// CAPRETURN: ticker amount TOTAL value [currency] EXPENSES expenses [currency]
capreturn_args = { ticker ~ quantity ~ "TOTAL" ~ money ~ "EXPENSES" ~ money }

// SPLIT/UNSPLIT: ticker RATIO ratio
split_args = { ticker ~ "RATIO" ~ ratio }

ticker = @{ ASCII_ALPHANUMERIC+ }
quantity = @{ decimal }
ratio = @{ decimal }
decimal = @{ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }

// Whitespace between tokens (no newlines - transactions must be on separate lines)
WHITESPACE = _{ " " | "\t" }
COMMENT = { "#" ~ (!NEWLINE ~ ANY)* }
NEWLINE = _{ "\r\n" | "\n" | "\r" }
